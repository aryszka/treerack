// JSON (http://www.json.org)
whitespace:ws    = [ \b\f\n\r\t];
true             = "true";
false            = "false";
null             = "null";
string:nows      = "\"" ([^\\"\b\f\n\r\t] | "\\" (["\\/bfnrt] | "u" [0-9a-f]{4}))* "\"";
number:nows      = "-"? ("0" | [1-9][0-9]*) ("." [0-9]+)? ([eE] [+\-]? [0-9]+)?;
entry            = string ":" expression;
object           = "{" (entry ("," entry)*)? "}";
array            = "[" (expression ("," expression)*)? "]";
expression:alias = true | false | null | string | number | object | array;
json:root        = expression;
